open "basics";;


open "Finite_Set_S";;
open "L_Assemblee_nationale_S";;
open "L_Electorat_S";;
open "Le_Parlement_S";;
open "Le_Senat_S";;
open "Un_Acte_de_l_executif_S";;
open "Un_Bureau_de_vote_S";;
open "Un_Citoyen_electeur_S";;
open "Un_Citoyen_electeur_subtype_S";;
open "Un_Decret_S";;
open "Un_Depute_S";;
open "Un_Domaine_de_projet_legislatif_S";;
open "Un_Elu_S";;
open "Un_Elu_subtype_S";;
open "Un_Etre_humain_S";;
open "Un_Francais_S";;
open "Un_Parlementaire_S";;
open "Un_Parlementaire_subtype_S";;
open "Un_Scrutin_S";;
open "Un_Senateur_S";;
open "Un_Territoire_S";;
open "Un_Territoire_francais_S";;
open "Un_Texte_legislatif_S";;
open "Une_Loi_S";;
open "Une_Ordonnance_S";;
open "Une_Origine_S";;
open "Une_Proposition_de_loi_S";;
open "Une_Race_S";;
open "Une_Religion_S";;

--  Un_Depute           Un_Senateur
--      \                  /
--       v                v
--        Un_Parlementaire            Le_President_de_la_republique Un_Elu_europeen Un_Elu_regional Un_Elu_cantonal Un_Elu_municipal  
--                |
--                v
--             Un_Elu
--                |
--                v
--           Un_Francais

species Article_24_S (
  URac is Une_Race_S,
  UOri is Une_Origine_S,
  URel is Une_Religion_S,
  UFra is Un_Francais_S (URac, UOri, URel),
  UFra_set is Finite_Set_S (UFra),
  UScr is Un_Scrutin_S,
  UTer is Un_Territoire_S,
  UTfr is Un_Territoire_francais_S,
  UTle is Un_Texte_legislatif_S,
  UOrd is Une_Ordonnance_S,
  UDec is Un_Decret_S,
  UDpl is Un_Domaine_de_projet_legislatif_S,
  UBvo is Un_Bureau_de_vote_S (UTfr), 
  UCel is Un_Citoyen_electeur_S (URac, UOri, URel, UTfr, UScr, UBvo),
  UCel_set is Finite_Set_S (UCel),
  UElu is Un_Elu_S (URac, UOri, URel, UTfr, UScr, UBvo),
  LEle is L_Electorat_S (URac, UOri, URel, UTfr, UScr, UBvo, UCel),
  UElu_set is Finite_Set_S (UElu),
  UAex is Un_Acte_de_l_executif_S,
  UDep is Un_Depute_S (URac, UOri, URel, UTfr, UScr, UBvo),
  UDep_set is Finite_Set_S (UDep),
  USen is Un_Senateur_S (URac, UOri, URel, UTfr, UScr, UBvo),
  USen_set is Finite_Set_S (USen),
  UPar is Un_Parlementaire_S (URac, UOri, URel, UTfr, UScr, UBvo),
  UPar_set is Finite_Set_S (UPar),
  LAna is L_Assemblee_nationale_S (URac, UOri, URel, UTfr, UScr, UBvo, UDep, UPar),
  LSen is Le_Senat_S (URac, UOri, URel, UTfr, UScr, UBvo, USen, UPar),
  LPar is Le_Parlement_S (URac, UOri, URel, UTfr, UScr, UBvo, UDep, USen, UElu, UPar, LAna, LSen),
  ULoi is Une_Loi_S (URac, UOri, URel, UTfr, UScr, UBvo, UDep, USen, UElu, UPar, LAna, LSen, LPar),
  UPlo is Une_Proposition_de_loi_S (URac, UOri, URel, UTle, UOrd, UDec, UTfr, UScr, UDpl, UBvo, UFra, UCel, UDep, USen, LEle, UElu, UPar, LAna, LSen, LPar),
  UParOfUDep is Un_Parlementaire_subtype_S (URac, UOri, URel, UTfr, UScr, UBvo, UDep, UDep_set, UPar, UPar_set),
  UParOfUSen is Un_Parlementaire_subtype_S (URac, UOri, URel, UTfr, UScr, UBvo, USen, USen_set, UPar, UPar_set),
  UEluOfUPar is Un_Elu_subtype_S (URac, UOri, URel, UTfr, UScr, UBvo, UPar, UPar_set, UElu, UElu_set),
  UCelOfUElu is Un_Citoyen_electeur_subtype_S (URac, UOri, URel, UTfr, UScr, UBvo, UElu, UElu_set, UCel, UCel_set)
) = 

  signature l_assemblee_nationale : Self -> LAna;

  signature les_deputes : Self -> UDep_set;
  
  signature le_senat : Self -> LSen;

  signature les_senateurs : Self -> USen_set;

  signature les_francais : Self -> UFra_set;

  -- Titre IV Article 24-a-1 - Le Parlement vote la uloi. 

  property article_24_a_1 : all uloi : ULoi, 
    ULoi!une_loi_est_adoptee_par_l_assemblee_nationale (uloi)
    /\ 
    ULoi!une_loi_est_adoptee_par_le_senat (uloi);

  -- Titre IV Article 24-a-2 - Le Parlement contrôle l'action du Gouvernement. 

  property article_24_a_2 : all lpar : LPar,
    LPar!le_gouvernement_est_responsable_devant_le_parlement (lpar); 

  -- Titre IV Article 24-a-3 - Le Parlement évalue les politiques publiques.

  property article_24_a_3 : all lpar : LPar,
    LPar!le_parlement_evalue_les_politiques_publiques (lpar); 

  -- Titre IV Article 24-b - Le Parlement comprend l'Assemblée nationale et le Sénat.

  let le_parlement (self : Self) : UPar_set =
      UPar_set!union (UParOfUDep!to_super_set (les_deputes (self)), 
                      UParOfUSen!to_super_set (les_senateurs (self))) ;

  -- Titre IV Article 24-c-1 - Les députés à l'Assemblée nationale, dont le nombre ne peut excéder cinq cent soixante-dix-sept, ...

  logical let le_nombre_de_deputes_est_inferieur_ou_egal_a_577 (lana: LAna) =
    LAna!cardinal (lana) <= 577;

  -- Titre IV Article 24-c-2 - [ Les députés à l'Assemblée nationale ] ..., sont élus au suffrage direct

  property article_24_c : 
  all lana : LAna, all udep : UDep, all uscr : UScr,
  (le_nombre_de_deputes_est_inferieur_ou_egal_a_577 (lana)) 
  /\
  UDep!est_elu_au_suffrage_direct (udep, uscr); 

  -- Titre IV Article 24-d-1 - Le Sénat, dont le nombre de membres ne peut excéder trois cent quarante-huit, ...

  logical let le_nombre_de_senateur_est_inferieur_ou_egal_a_348 (lsen: LSen) =
    LSen!cardinal (lsen) <= 348;

  -- Titre IV Article 24-d-2 - [ Le Sénat ] ..., est élu au suffrage indirect.

  property article_24_d : 
  all lsen : LSen, all uelu : UElu,
  (le_nombre_de_senateur_est_inferieur_ou_egal_a_348 (lsen)) 
  /\
  (UElu!est_elu_au_suffrage_indirect (uelu));

  -- Titre IV Article 24-c-2 Il assure la représentation des collectivités territoriales de la République.

  -- Titre IV Article 24-e - Les Français établis hors de France sont représentés à l'Assemblée nationale et  au Sénat.

  logical let les_francais_etablis_hors_de_france (self : Self) =
    all ufra : UFra,
    UFra_set!is_a_member_of (ufra, les_francais (self))
    /\
    UFra!reside_en_permanence_a_l_etranger (ufra);

  property article_24_e : all self : Self, all udep : UDep, all usen : USen,
  les_francais_etablis_hors_de_france (self) ->
  ((UDep!un_depute_represente_des_francais (udep)) 
  /\
  (USen!un_senateur_represente_des_francais (usen)));

end;;